plugins {
	id 'org.springframework.boot' version '2.7.0'
	id 'java'
	id 'jacoco'
}






repositories {
	mavenCentral()
}

bootJar.enabled = false

subprojects {
	group = 'com.jung'
	version = '0.0.1-SNAPSHOT'
	sourceCompatibility = '11'

	apply plugin: 'java'
	// build.gradle에서 api() 를 사용하려면 java-library 사용
	apply plugin: 'java-library'
	apply plugin: 'org.springframework.boot'
	// spring boot dependency를 사용하여 사용중인 부트 버전에서 자동으로 의존성을 가져온다.
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'jacoco'

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	repositories {
		mavenCentral()
	}

	// 관리하는 모듈에 공통 dependencies
	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation 'org.springframework.boot:spring-boot-starter-validation'
		implementation 'org.springframework.boot:spring-boot-starter-data-redis'
		implementation 'mysql:mysql-connector-java'

		compileOnly 'org.projectlombok:lombok'
		developmentOnly 'org.springframework.boot:spring-boot-devtools'

		annotationProcessor 'org.projectlombok:lombok'
		//테스트 환경 설정
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testImplementation 'com.h2database:h2:2.1.214'
	}

	test {
		useJUnitPlatform()
		finalizedBy 'jacocoTestReport'
	}

	jacoco {
		toolVersion = '0.8.9'
	}

	jacocoTestReport {
		reports {
			html.enabled true
			xml.enabled false
			csv.enabled false
		}
		finalizedBy 'jacocoTestCoverageVerification'
	}

	jacocoTestCoverageVerification {
		violationRules {
			rule {
				element = 'CLASS'

				limit {
					counter = 'BRANCH'
					value = 'COVEREDRATIO'
					minimum = 0.70
				}
			}
		}
	}
}

project(':common') {
	dependencies {
		implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
		implementation 'com.googlecode.json-simple:json-simple:1.1.1'

	}
}

project(':domain') {
	dependencies {
		implementation project(':common')
		implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
		implementation 'com.googlecode.json-simple:json-simple:1.1.1'

	}
}

project(':module-user') {
	dependencies {
		implementation project(':common')
		implementation project(':domain')
		implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
		implementation 'com.googlecode.json-simple:json-simple:1.1.1'
		implementation 'org.springframework.session:spring-session-data-redis'


	}
}

project(':module-api-usage') {
	dependencies {
		implementation project(':common')
		implementation project(':domain')
		implementation 'redis.clients:jedis:4.3.1'
		implementation 'org.springframework.boot:spring-boot-starter-batch'
		implementation 'org.springframework.boot:spring-boot-starter-quartz'

	}
}

project(':module-product') {
	dependencies {
		implementation project(':common')
		implementation project(':domain')
		implementation 'redis.clients:jedis:4.3.1'
		implementation 'com.h2database:h2:2.1.214'
		implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
		implementation 'com.googlecode.json-simple:json-simple:1.1.1'
	}
}

project(':module-comparecart') {
	dependencies {
		implementation project(':common')
		implementation project(':domain')
	}
}

project(':module-filterkeyword') {
	dependencies {
		implementation project(':common')
		implementation project(':domain')
		implementation 'com.googlecode.json-simple:json-simple:1.1.1'
	}
}

project(':module-webpage') {
	dependencies {
		implementation project(':common')
		implementation project(':domain')
		implementation 'org.springframework.boot:spring-boot-starter-thymeleaf:2.7.3'
	}
}
